plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    defaultConfig {
        applicationId "dev.hossain.android.catalog"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode 1
        versionName "1.0"

        versionNameSuffix "-${gitSha()}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        testOptions {
            unitTests.returnDefaultValues = true
        }
    }
    buildTypes {
        release {
            // https://developer.android.com/studio/build/shrink-code
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        // Java 8 support for Kotlin projects
        // https://developer.android.com/studio/write/java8-support
        jvmTarget = "1.8"
    }

    buildFeatures { // https://developer.android.com/studio/releases/gradle-plugin#buildFeatures
        // https://developer.android.com/topic/libraries/data-binding/start
        dataBinding = true
    }

    testOptions {
        // https://developer.android.com/training/testing/fundamentals
        unitTests {
            includeAndroidResources = true
        }
    }
    // Gradle automatically adds 'android.test.runner' as a dependency.
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

// Configure ktlint plugin
// Use `./gradlew ktlintFormat` command to format code.
// https://github.com/jlleitschuh/ktlint-gradle#configuration
ktlint {
    debug = true
    android = true
}

kapt {
    correctErrorTypes true
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Google Products & Support library dependencies
    // --------------------------------------------------
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation "androidx.core:core-ktx:$rootProject.coreKtxVersion"
    implementation "com.google.android.material:material:$rootProject.androidMaterialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "androidx.viewpager2:viewpager2:$rootProject.viewPager2Version"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycleVersion"
    // alternately - if using Java8, use the following instead of lifecycle-compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:$rootProject.lifecycleVersion"
    implementation "androidx.fragment:fragment-ktx:1.2.5"


    // Kotlin Coroutines on Android
    // https://developer.android.com/kotlin/coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'

    // Firebase and Crashlytics platform
    // https://firebase.google.com/docs/android/setup
    // https://firebase.google.com/docs/android/learn-more#bom
    // --------------------------------------------------
    implementation platform('com.google.firebase:firebase-bom:26.2.0')

    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics'

    //
    // 3rd-Party Libraries
    // --------------------------------------------------

    // Timber for logging
    implementation "com.jakewharton.timber:timber:$rootProject.timberLibraryVersion"

    // Leak canary - memory leak (DEBUG only)
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanaryLibraryVersion"

    // Retrofit - https://github.com/square/retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"

    // OkHttp Client - https://github.com/square/okhttp
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okHttpVersion"

    // Gson for JSON
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // --------------------------------------------------
    // ReactiveX / RxJava stream related libraries
    // --------------------------------------------------
    implementation "io.reactivex.rxjava3:rxandroid:$rootProject.rxAndroidVersion"
    implementation "io.reactivex.rxjava3:rxjava:$rootProject.rxJavaVersion"

    // Dagger Hilt (dependency injection)
    // https://developer.android.com/training/dependency-injection/hilt-android#setup
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    // Hilt and Jetpack integrations
    // https://developer.android.com/training/dependency-injection/hilt-jetpack
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$rootProject.androidXHilt"
    kapt "androidx.hilt:hilt-compiler:$rootProject.androidXHilt"

    // Parser for Device Catalog CSV file
    // https://github.com/amardeshbd/android-device-catalog-parser
    implementation 'com.github.amardeshbd:android-device-catalog-parser:1.1'

    // Room Database
    // https://developer.android.com/training/data-storage/room
    implementation "androidx.room:room-runtime:$rootProject.androidXRoomVersion"
    kapt "androidx.room:room-compiler:$rootProject.androidXRoomVersion"
    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$rootProject.androidXRoomVersion"


    //
    // Unit Tests
    // --------------------------------------------------
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "androidx.test:core:$rootProject.androidxTestCore"
    testImplementation "androidx.test:runner:$rootProject.androidxTestCore"
    testImplementation "androidx.test:rules:$rootProject.androidxTestCore"
    testImplementation "androidx.arch.core:core-testing:$rootProject.androidxArchCoreTesting"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$rootProject.mockitoVersion"
    testImplementation "androidx.room:room-testing:$androidXRoomVersion"

    androidTestImplementation "androidx.test:runner:$rootProject.androidTestRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.ext:truth:1.3.0'
    androidTestImplementation 'com.google.truth:truth:1.0'
}